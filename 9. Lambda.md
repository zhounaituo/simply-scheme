1. Lambda 用于生产过程的特殊形式（special form）。它接受“有关如何创建函数”的信息为参数，并返回一个没有名字的过程。
> 特殊形式（special form）意味着它不会理解计算它的参数。
1. define
	1. define 是一个缩写，它包含了两个过程：
		1. 创建过程
		2. 绑定名称
	2. define 真正的作用是给某个东西起个名字。
```scheme
(define (square x)
  (* x x))
(define square (lambda (x) (* x x)))
```
	3. 计算表达式时，表达式的每个值都会被替换成某个值。例如，乘法过程被命名为 `*`
2. let
	1. let 被定义为一个辅助过程，用来命名常用值。
	2. let 为了不免常见无用的命名过程。
	3. let  包含两个过程：
		1. 创建过程
		2. 调用(invocation)
3. 当出现参数名称时，内部的数据优先。
```scheme
(define (f x)
  (lambda (x) (+ x 3)))
```
4. 何时使用未命名函数
	1. 命名的好处是
		1. 简洁代码，让人易于理解。
		2. 有意义的名称可以让人从对它工作过程的关注转移到目的上，也就是抽象。
	2. 如果有一个过程只需要用一次，那就可以使用 lambda 了。
5. 陷阱
	1. 使用 define 定义常数时，不要给名字括号。
		```scheme
		(define (pi) 3.14159) ; 不要这样这，这里创建了一个过程。
		```
	2. 最好以函数的范围作为是否使用lamdba的依据。
	3. 立刻调用 lamdba 表达式：
	```scheme
	((lambda (x) (+ x 3)) 6)
	```

## Boring Exercises

9.1. 下面的程序将打印什么
```scheme
> (lambda (x) (+ (* x 3) 4)) ; 一个函数
> ((lambda (x) (+ (* x 3) 4)) 10) ; 34
> (every (lambda (wd) (word (last wd) (bl wd)))
         '(any time at all)) ; (yan etim ta lal)
> ((lambda (x) (+ x 3)) 10 15) ; error 不能有两个参数。
```

9.2. 使用 lambda 重新定义。
```scheme
(define (second stuff)
  (first (bf stuff)))
(define second (lambda (stuff) (first (bf stuff))))
```
```scheme
(define (make-adder num)
  (lambda (x) (+ num x)))
(define make-adder (lambda (num) (lambda (x) (+ num x))))
```

9.3. 解释一下下列程序
```scheme
(define (let-it-be sent)
  (accumulate (lambda (x y) y) sent))
```

答：取出 sent 中的 最后一个值。

## Real Exercises
9.4. 修复一下程序
```scheme
; 方案一
(define (who sent)
  (every (describe sent) '(pete roger john keith)))
(define (describe sent)
  (lambda (person) (se person sent)))
```
```scheme
; 方案二
(define (who sent)
  (every (lambda (person) (se person sent)) '(pete roger john keith)))
```

9.5. 编写 prepend-every 
```scheme 
(define (prepend-every pre sent)
  (every (lambda (wd) (word pre wd)) sent))
```

9.6. 编写 sentene-version，将函数 f 作为其参数并返回函数 g。g 应该将一个句子作为参数，并返回通过将 f 应用于该参数的每个单词而形成的句子。
```scheme
(define (sentence-version f)
  (lambda (sent) (every f sent)))
```

9.7. 编写 letterwords，该过程以一个字母和一个句子为参数。它返回一个句子，该句子仅包含包含参数字母的参数句中的那些单词：
```scheme
(define (letterwords letter sent)
  (keep (lambda (wd) (member? letter wd)) sent))
```

9.8. 编写 hang，一个过程，该过程将秘密单词和到目前为止猜到的字母作为参数，通过包含所有猜到的字母以及尚未猜到的字母的下划线来返回显示猜谜进度的单词：
```scheme
(define (hang wd letters)
  (accumulate word (every (lambda (x) (if (member? x letters) x '_ )) wd)))
```

9.9. 编写 common-wrods ，将两个句子作为参数，返回同时出现在两个句子中的单词。
```scheme
(define (common-words sent1 sent2)
  (keep (lambda (wd) (member? wd sent1)) sent2))
```

9.10. 重新实现 appearances 
```scheme
(define (appearances letter wd)
  (count (keep (lambda (x) (equal? x letter)) wd)))
```
> 原函数并不是统计 参数1 出现的次数，而是 参数1 作为 参数2 成员次数。

```scheme
(appearances 'a '(a aa)) ; 结果是 1 而不是 3.
```

9.11. 编写 unabbrev，由两个句子作为参数，返回第一个句子，但是数字替换为第二个句子中相应的单词。
```scheme
(define (unabbrev sent1 sent2)
  (every (lambda (wd) (if (number? wd) (item wd sent2) wd)) sent1))
```

9.12. 编写 first-last ，其参数将是一个句子。它应该返回一个句子，仅首尾相同的单词：
```scheme
(define (first-last sent)
  (keep (lambda (wd) (equal? (first wd) (last wd))) sent))
```

9.13. 编写 compose, 将两个函数 f 和 g 作为参数。它应该返回一个新函数，即其输入函数的组合，该函数在传递参数 x 时计算 f（g（x））。
```scheme
(define (compose fx gx)
  (lambda (arg) (fx (gx arg))))
```

9.14. 编写 substitute，包含三个参数、两个单词和一个句子。它应该返回句子的一个版本，但第二个单词的每个实例都替换为第一个单词。
```scheme
(define (substitute wd1 wd2 sent)
  (every (lambda (wd) (if (equal? wd2 wd) wd1 wd)) sent))
```

9.15. 编写 type-check，返回一个函数，由一个过程和一个判断过程构成。执行时会优先执行 参数2 进行判断，如果判断通过则，将参数给 参数1 执行；否则返回 #f。
```scheme
(define (type-check f pred)
  (lambda (arg) (if (pred arg) (f arg) #f)))
```

9.16. 编写 aplize，该过程将域为数字或单词的单参数作为其参数，它应该返回一个 APLized 过程。
```scheme
(define (aplize fx)
  (lambda (arg) (if (sentence? arg) (every fx arg) (fx arg))))
```

9.17. 用 accumulate 和 every 编写 keep。
```scheme
(define (keep pred? arg)
  (accumulate (if (word? arg)
                  word
                  sentence)
              (every (lambda (param) (if (pred? param) param '())) arg)))
```
> 注意，keep 总是返回与参数 2 相同的类型。
